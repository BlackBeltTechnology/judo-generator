[%
import "operations/_importAll.eol";

var COORDINATE_TO_TEMPLATE = Map {
		JAVA!JvmJudoCoordinate#PO = "po",
		JAVA!JvmJudoCoordinate#PO_ENUM = "enum",

		JAVA!JvmJudoCoordinate#BO = "bo",
		JAVA!JvmJudoCoordinate#BO_ENUM = "enum",
		JAVA!JvmJudoCoordinate#BO_BUILDER = "bobuilder",
		JAVA!JvmJudoCoordinate#BO_BQL = "bobql",
		JAVA!JvmJudoCoordinate#DOMAIN_BQL = "domainbql",
		//JAVA!JvmJudoCoordinate#BO_TYPE_RESOLVER = "botyperesolver"

		JAVA!JvmJudoCoordinate#DAO_MINIMAL = "daominimal",

		JAVA!JvmJudoCoordinate#BO_SERVICE_INTERFACE = "boservice",
		
		JAVA!JvmJudoCoordinate#BO_SERVICE_ABSTRACT = "boserviceabstract",
		JAVA!JvmJudoCoordinate#BO_SERVICE_DEFAULT = "boservicedefault",
		JAVA!JvmJudoCoordinate#BO_ENUM_SERVICE = "boenumservice",
		JAVA!JvmJudoCoordinate#BO_SERVICE_WRAPPER = "boservicewrapper",

		JAVA!JvmJudoCoordinate#DAO_QUERY_DSL = "daoquerydsl",

		JAVA!JvmJudoCoordinate#DTO = "dto",
		JAVA!JvmJudoCoordinate#DTO_BUILDER = "dtobuilder",
		JAVA!JvmJudoCoordinate#DTO_ENUM = "dtoenum",
		JAVA!JvmJudoCoordinate#DTO_OPERATION = "dtooperation",
		JAVA!JvmJudoCoordinate#DTO_ACCESSOR = "dtoaccessor",
		JAVA!JvmJudoCoordinate#DTO_OPERATION_IMPL = "dtooperationimpl",

		JAVA!JvmJudoCoordinate#REST_DTO = "restdto",
		JAVA!JvmJudoCoordinate#REST_DTO_BUILDER = "restdtobuilder",
		JAVA!JvmJudoCoordinate#REST_DTO_ENUM = "restdtoenum",
		JAVA!JvmJudoCoordinate#REST_OPERATION = "restoperation",
		JAVA!JvmJudoCoordinate#REST_OPERATION_IMPL = "restoperationimpl"
		
	};

for (key in COORDINATE_TO_TEMPLATE.keySet()) {
	getJavaTemplate(COORDINATE_TO_TEMPLATE.get(key)).genJavaFor(key);
}


operation Template genJavaFor(coordinate : Any) : Any {
	for (coordinated in filterBy(coordinate)) {
		if (not coordinated.fwElement) {
			self.genJava(coordinated);
		}
	}
}

operation Template genJava(coordinated : JAVA!JvmCoordinated) : Any {
	self.populate("class", coordinated);
	self.generate(coordinated.coordinate.name + "/" + coordinated.fqPath() + ".java");
	self.reset();
}

operation getJavaMacro (macroName : String) : Any {
	return getTemplate("java/macros", macroName);
}

operation getJavaTemplate (templateName : String) : Any {
	return getTemplate("java", templateName);
}

operation getTemplate (intermediateDir : String, templateName : String) : Any {
	return TemplateFactory.load("templates/" + intermediateDir + "/" + templateName + ".egl");
}

operation filterBy (coordinate : Any) : Collection {
	var toBeRemovedCoordinatesHack = Set{
									JAVA!JvmJudoCoordinate#BO_SERVICE_INTERFACE,
									JAVA!JvmJudoCoordinate#BO_SERVICE_ABSTRACT,
									JAVA!JvmJudoCoordinate#BO_SERVICE_DEFAULT,
									JAVA!JvmJudoCoordinate#BO_SERVICE_WRAPPER
									};
	return JAVA!JvmCoordinated.all.select(c | c.coordinate = coordinate and not (c.framework and toBeRemovedCoordinatesHack.includes(c.coordinate)));
}

%]