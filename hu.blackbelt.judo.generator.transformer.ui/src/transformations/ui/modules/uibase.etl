rule UIBase
	transform jmodel: PSM!JModel
	to uibase : UI!UIBase {
	
	uibase.name = jmodel.name;
	uibase.uuid = jmodel.uuid;
	
	for (package : PSM!JPackage in PSM!JPackage.all) {
		var currentPackage : UI!UIModule = package.equivalent("UIModule");
		if (currentPackage <> null) {
			uibase.modules.add(currentPackage);
		}
	}
	
	var mainMenu = new UI!UIMenuItem();
	mainMenu.uuid = "main_menu";
	mainMenu.name = "main_menu";
	
	for (menuRoot in jmodel.rootMenuItems) {
		for (menu in menuRoot.children) {
			mainMenu.menuItems.add(menu.equivalent("UIMenuItem"));
		}
	}
	
	uibase.mainMenu = mainMenu;
	
	var uiInfo = new UI!UIInfo();
	if (jmodel.info.isDefined()) {
		for (submodel in jmodel.info.submodels) {
			if (submodel.version.isDefined()) {
				uiInfo.versions.add(submodel.version);
			} else {
				uiInfo.versions.add("");
			}
			
			uiInfo.submodels.add(submodel.name);
		}
		for (jclass in PSM!JClass.all) {
			if (jclass.representsUser) {
				uiInfo.userClass = jclass.uuid;
			}
		}
	}
	
	uiInfo.name = "main_info";
	uiInfo.uuid = "main_info";
	
	uibase.info = uiInfo;
}