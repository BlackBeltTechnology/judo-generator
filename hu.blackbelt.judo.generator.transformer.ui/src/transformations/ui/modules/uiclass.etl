import "../../../operations/_importAll.eol";

rule UIClass
	transform jclass : PSM!JClass
	to uiclass : UI!UIClass {
	guard : jclass.package.participates(PSM!JLayer#OPERATION) or jclass.package.participates(PSM!JLayer#SCREEN) 
	//inherited from Identifiable
	uiclass.uuid = jclass.uuid;
	uiclass.name = jclass.name;
	
	//Attributes
	uiclass.abstract = jclass.abstract;
	uiclass.enumClass = jclass.representsEnum;
	uiclass.singleton = jclass.businessSingleton;
	if (jclass.visibility = PSM!JVisibility#PROTECTED or jclass.visibility = PSM!JVisibility#PRIVATE or jclass.visibility = PSM!JVisibility#PACKAGE) {
		uiclass.readonly = true;
	} else {
		uiclass.readonly = false;
	}
	
	//References
	uiclass.representation = jclass.representation.equivalent("UIBaseComponentType");
	uiclass.super = jclass.supertype.equivalent("UIClass");
	
	//create Actions
	var inherited : Boolean = false;
	var currentClass : PSM!JClass = jclass;
	while (true) {
		for (op : PSM!JOperation in currentClass.operations) {
			if (op.visibility = PSM!JVisibility#PUBLIC) {
				var action : UI!UIAction = op.equivalent("UIAction");
				action.inherited = inherited;
				uiclass.actions.add(action);
			}	
		}
			
		if (currentClass.supertype.isDefined()) {
			currentClass = currentClass.supertype;
			inherited = true;
		}	else {
			break;
		}
	}

	//create ComponentTypes
	inherited = false;
	currentClass = jclass;
	while (true) {
		for (attr : PSM!JAttribute in currentClass.attributes) {
			var attribute : UI!UIBaseComponentType = attr.equivalent("UIBaseComponentType");
			attribute.inherited = inherited;
			if (uiclass.representation.isDefined() and uiclass.representation.uuid = attribute.uuid) {
				attribute.representation = true;
			}
			uiclass.attributes.add(attribute);
			}
		for (role : PSM!JRole in currentClass.roles) {
			var reference : UI!UIReferenceComponentType = role.equivalent("UIReferenceComponentType");
			reference.inherited = inherited;
			uiclass.attributes.add(reference);
		}
		
		if (currentClass.supertype.isDefined()) {
			currentClass = currentClass.supertype;
			inherited = true;
		}	else {
			break;
		}
		
	}

	//create Views
	var classView = createClassView(uiclass, jclass, uiclass.name, uiclass.uuid + '_oview_default', true);
	uiclass.selector = createListView(uiclass, jclass, uiclass.name, uiclass.uuid + '_lview_default', classView, true);
	uiclass.classViews.add(classView);
	uiclass.listViews.add(uiclass.selector);
	for (al in jclass.aliases) {
		classView = createClassView(uiclass, jclass, al.name, uiclass.uuid + '_oview_' + al.name, false);
		var listView =  createListView(uiclass, jclass, al.name, uiclass.uuid + '_lview_' + al.name, classView, false);
		uiclass.classViews.add(classView);
		uiclass.listViews.add(listView);
	}
		
}
	
	
operation createClassView(ownerClass: UI!UIClass, originClass: PSM!JClass, name: String, uuid: String, isDefault: Boolean) : UI!UIClassView {
	var classView : UI!UIClassView = new UI!UIClassView();
	classView.uuid = uuid;
	classView.name = name;
	classView.isDefault = isDefault;
	
	var position = 1;
	var remaining = ownerClass.attributes;
	var vfsName : String;
	
	for (attrGroup in originClass.attributeOrder) {
		if (attrGroup.attributes.first().name.startsWith("-")) {
			vfsName = attrGroup.attributes.first().name.substring(1);
			attrGroup.attributes.remove(attrGroup.attributes.first());
		} else {
			vfsName = attrGroup.attributes.first().name;
		}
		
		var viewFieldSet = new UI!UIViewFieldSet();
		viewFieldSet.name = vfsName;
		viewFieldSet.uuid = classView.uuid + "_viewfieldset_" + vfsName;
		viewFieldSet.position = position;
		
		var viewFieldPosition = 1;
		for (attribute in attrGroup.attributes) {
			var viewField = new UI!UIViewField();
			viewField.componentType = attribute.equivalent();
			viewField.name = attribute.name;
			viewField.uuid = attribute.uuid + name;
			viewField.searchable = true;
			viewField.position = viewFieldPosition;
			viewFieldPosition++;
			
			remaining.remove(attribute);
			
			viewFieldSet.viewFields.add(viewField);
		}
		
		position++;
		classView.viewFieldSets.add(viewFieldSet);
	}
	
	for (attribute in remaining) {
		if ((attribute.isTypeOf(UI!UIReferenceComponentType) and attribute.navigable and not attribute.private) 
													or (attribute.isTypeOf(UI!UIBaseComponentType) and not attribute.private)) {
			var viewFieldSet : UI!UIViewFieldSet = new UI!UIViewFieldSet();
			viewFieldSet.name = attribute.name;
			viewFieldSet.uuid = classView.id + "_viewfieldset_" + attribute.name;
			viewFieldSet.position = position;
			
			var viewField : UI!UIViewField = new UI!UIViewField();
			viewField.name = attribute.name;
			viewField.uuid = attribute.id + name;
			viewField.componentType = attribute;
			viewField.searchable = true;
			
			position++;
			viewFieldSet.viewFields.add(viewField);
			classView.viewFieldSets.add(viewFieldSet);
		}
	}
	
	return classView;
}

operation createListView(ownerClass: UI!UIClass, originClass: PSM!JClass, name: String, uuid: String, classView: UI!UIClassView, isDefault: Boolean) : UI!UIListView {
	var listView = new UI!UIListView();
	listView.uuid = uuid;
	listView.name = name;
	listView.isDefault = isDefault;
	listView.pageSize = 20;
	listView.detailView = classView;
	
	var viewFieldSet = new UI!UIViewFieldSet();
	viewFieldSet.name = listView.name;
	viewFieldSet.uuid = listView.uuid + "_viewfieldset_" + listView.name;
	
	var position = originClass.attributesForListing.size();
	
	for (attribute in originClass.attributes) {
		var uiType = attribute.equivalent();
		if ((uiType.isTypeOf(UI!UIReferenceComponentType) and uiType.upper = 1 and uiType.navigable and not uiType.private) or 
				(uiType.isTypeOf(UI!UIBaseComponentType) and not uiType.private)) {
				
				var viewField = new UI!UIViewField();
				viewField.uuid = uiType.uuid + ownerClass.name;
				viewField.name = uiType.name;
				viewField.searchable = true;
				viewField.componentType = uiType;
				
				if (originClass.attributesForListing.includes(attribute)) {
					viewField.position = originClass.attributesForListing.indexOf(attribute) + 1;
				} else {
					if (originClass.attributesForListing.size() > 0) {
						viewField.show = false;
					}
					position += 1;
					viewField.position = position;
				}
				
				viewFieldSet.viewFields.add(viewField);
		}
	}
	
	listView.viewFieldSets.add(viewFieldSet);
	
	return listView;
}