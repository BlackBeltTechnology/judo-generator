import "../../operations/_importAll.eol";
import "element.etl";

rule Operation
	transform s : Operation
	to 		  t : tgtPSM!JOperation 
	extends NamedElement {
	
	guard: s.owner.equivalent("Class").isDefined()
	
	s.owner.equivalent("Class").operations.add(t);
	
	if ( stereotypeApplications.get("query").includes(s) ) {
		t.kind = tgtPSM!JOperationKind#QUERY;
	} else {
		t.kind = tgtPSM!JOperationKind#CUSTOM;
	}
	t.uiMustConfirm = not s.stereotype("nonconfirmedOperation");
	
	t.classBased = s.static;
	
	if ((t.kind = tgtPSM!JOperationKind#CUSTOM) and
		not t.classBased) {
		t.bulk = true;
	} else {
		t.bulk = false;
	}
	
	var accessBy : Sequence = stereotypeApplications.get("accessDef").get(s);
	if (accessBy.isDefined() and accessBy.size() > 0) {
		for (a in accessBy) { 
			t.accessibleWith.add(a.equivalent());
		}
	}
}