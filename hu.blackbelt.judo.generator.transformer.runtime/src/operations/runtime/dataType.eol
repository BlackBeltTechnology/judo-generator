@cached
operation String getJudoDataType() {
	return TYPES!EDataType.all.selectOne(dt | dt.name == self);
}

operation JUDOPSM!DataType createRuntimeAnnotations() {
	var annotations = Sequence{};

	if (self.isKindOf(JUDOPSM!String)) {
		if (self.maxLength.isDefined()) {
			var aMaxLength = new RUNTIME!EStringToStringMapEntry;
			aMaxLength.key = "maxLength";
			aMaxLength.value = self.maxLength.asString();
			annotations.add(aMaxLength);
		}
	} else if (self.isKindOf(JUDOPSM!Numeric)) {
		// TODO: add annotations: minValue, maxValue
	}

	return annotations;
}

@cached
operation JUDOPSM!DataType getEcoreType() {
	if (self.isKindOf(JUDOPSM!String)) {
		return "JString".getJudoDataType();
	} else if (self.isKindOf(JUDOPSM!Numeric)) {
		if (self.precision == 0 and self.scale <= 9) {
			return "JInteger".getJudoDataType();
		} else if (self.precision == 0 and self.scale > 9 and self.scale <= 18) {
			return "JLong".getJudoDataType();
		} else if (self.precision == 0 and self.scale > 18) {
			return "JBigInteger".getJudoDataType();
		} else if (self.precision > 0 and self.scale <= 7) {
			return "JFloat".getJudoDataType();
		} else if (self.precision > 0 and self.scale > 7 and self.scale <= 15) {
			return "JDouble".getJudoDataType();
		} else if (self.precision > 0 and self.scale > 15) {
			return "JBigDecimal".getJudoDataType();
		}
	} else if (self.isKindOf(JUDOPSM!Boolean)) {
		return "JBoolean".getJudoDataType();
	} else if (self.isKindOf(JUDOPSM!Enumeration)) {
		return self.equivalent();
	} else if (self.isKindOf(JUDOPSM!Custom)) {
		switch (self.name) {
			case "Date":
				return "JDate".getJudoDataType();
			case "Timestamp":
				return "JTimestamp".getJudoDataType();
			case "Binary":
				return "JByteArray".getJudoDataType();
			default:
				// TODO: set original type (name) in annotation
				return "JObject".getJudoDataType();
		}
	}

	return null;
}