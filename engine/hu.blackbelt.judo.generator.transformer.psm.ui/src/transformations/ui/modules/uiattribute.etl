rule UIAttributeFromJAttribute
	transform jattribute : PSM!JAttribute
	to uiattribute : UI!UIAttribute {
		//inherited from Identifiable
		uiattribute.uuid = jattribute.uuid;
		uiattribute.name = jattribute.name;
		//attributes
		if (jattribute.visibility = PSM!JVisibility#PUBLIC) {
			uiattribute.readonly = false;
		}	else {
			uiattribute.readonly = true;
		}
		if (jattribute.visibility = PSM!JVisibility#PRIVATE) {
			uiattribute.private = true;
		}
		if (jattribute.derived or jattribute.calculated or jattribute.visibility = PSM!JVisibility#PROTECTED) {
			uiattribute.value = "";
		} else {
			uiattribute.value = jattribute.value;
		}
		uiattribute.derived = jattribute.derived;
		uiattribute.lower = jattribute.lower;
		uiattribute.upper = jattribute.upper;
		uiattribute.type = jattribute.class.name;
		uiattribute.regexp = jattribute.regexp;
		uiattribute.decimals = jattribute.decimals;
		uiattribute.placeholder = jattribute.placeholder;
		//references
		uiattribute.componentType = jattribute.type.equivalent();
	} 

rule UIAttributeFromJParameter
	transform jparameter : PSM!JParameter
	to uiattribute : UI!UIAttribute {
		//inherited from Identifiable
		uiattribute.uuid = jparameter.uuid;
		uiattribute.name = jparameter.name;
		//attributes
		uiattribute.value = "";
		uiattribute.lower = jparameter.lower;
		uiattribute.upper = jparameter.upper;
		uiattribute.type = jparameter.class.name;
		uiattribute.regexp = "";
		//references
		uiattribute.componentType = jparameter.type.equivalent();
		if(jparameter.type.isTypeOf(JClass)) {
			uiattribute.referenced = jparameter.type.equivalent();
		} else {
			uiattribute.referenced = null;
		}
	}