import "../../../operations/_importAll.eol";
import "element.etl";

@abstract
rule JAttributeToRdbmsField
	transform s : PSM!JAttribute
	to t : RDBMS!RdbmsField 
	extends JElementToRdbmsElement { 
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE)
	    if (not s.uuid.isDefined() or s.uuid.isEmpty()) {
	        t.uuid  = generateUuid(s.ownerClass.uuid, s.name, ".");
	    } else {
	        t.uuid = s.uuid;
	    }
		t.table = s.ownerClass.equivalent("JClassToRdbmsTable");

		t.rdbmsTypeName = s.type.rdbmsType().rdbmsTypeName;
		t.size = s.type.rdbmsType().size;
		t.precision = s.type.rdbmsType().precision;
		t.scale = s.type.rdbmsType().scale;
		t.storageByte = s.type.rdbmsType().storageByte;
				
		t.sqlName = s.fieldSqlName(); // fieldSqlName(s);
		t.name = s.ownerClass.package.fqName() + "." + s.ownerClass.name + "." + s.name;
		t.description = s.ownerClass.name + "." + s.name;
		
		t.originalName = s.ownerClass.name + "." + s.name;
		t.originalPackage = s.ownerClass.package.fqName();
		
		if (s.isPrimaryKey() or s.isGuid() or s.isXmiid()) {
			t.mandatory = true;
//		} else if (s.isAudit()) {
//			t.mandatory = false;
		} else {
			t.mandatory = s.mandatory;
		}
}


rule JAttributeToTableIdField
	transform s : PSM!JAttribute
	to t : RDBMS!RdbmsIdentifierField 
	extends JAttributeToRdbmsField { 
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and s.isPrimaryKey()
		t.table.primaryKey = t;
		t.sqlName = "ID";
		t.type = idType();
}

rule JAttributeToTableValueField
	transform s : PSM!JAttribute
	to t : RDBMS!RdbmsValueField
	extends JAttributeToRdbmsField { 
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and not s.isPrimaryKey() and not s.derived
		t.technical = s.isTechnical();
}

@lazy
rule JAttributeToViewIdFieldWithoutView
	transform s : PSM!JAttribute
	to v : RDBMS!RdbmsViewIdentifierField
	extends JElementToRdbmsElement { 
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and s.isPrimaryKey()
		v.name = s.ownerClass.package.fqName() + "." + s.ownerClass.name + ".id";
		v.sqlName = "ID";
		v.tableField = s.equivalent("JAttributeToTableIdField");
		v.uuid = generateUuid(v.tableField.uuid, "ViewField", "_");
		v.view = s.ownerClass.equivalent("JClassToRdbmsView");
		v.description = s.ownerClass.name + "." + s.name;
}

@lazy
rule JAttributeToViewIdField
	transform s : PSM!JAttribute
	to v : RDBMS!RdbmsViewIdentifierField 
	extends JAttributeToViewIdFieldWithoutView {
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and s.isPrimaryKey()
		v.view = s.ownerClass.equivalent("JClassToRdbmsView");
		v.`alias` = v.view.primaryTable;
}

@lazy
rule JAttributeToViewValueFieldWithoutView
	transform s : PSM!JAttribute
	to v : RDBMS!RdbmsViewValueField 
	extends JElementToRdbmsElement { 
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and not s.isPrimaryKey()
		v.tableField = s.equivalent("JAttributeToTableValueField");
		v.sqlName  = v.tableField.sqlName;
		v.uuid = generateUuid(v.tableField.uuid, "ViewField", "_");
		v.description = s.ownerClass.name + "." + s.name;
}

@lazy
rule JAttributeToViewValueField
	transform s : PSM!JAttribute
	to v : RDBMS!RdbmsViewValueField 
	extends JAttributeToViewValueFieldWithoutView { 
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and not s.isPrimaryKey()
		v.name = s.ownerClass.package.fqName() + "." + s.ownerClass.name + "." + s.name;
		v.view = s.ownerClass.equivalent("JClassToRdbmsView");
		v.`alias` = v.view.primaryTable;
		v.uuid = generateUuid(v.tableField.uuid, "ViewField", "_");
}

@lazy
rule JAttributeToViewExpressionField
	transform s : PSM!JAttribute
	to v : RDBMS!RdbmsViewExpressionField 
	extends JElementToRdbmsElement { 
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and not s.isPrimaryKey()
		v.name = s.ownerClass.package.fqName() + "." + s.ownerClass.name + "." + s.name;
  		v.expression = s.value;
		v.sqlName  = s.fieldSqlName();
		v.description = s.ownerClass.name + "." + s.name;
}

rule JAttributeToUniqueKey
	transform s : PSM!JAttribute
	to u : RDBMS!RdbmsUniqueConstraint 
	extends JElementToRdbmsElement { 
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and s.unique == true and not s.isPrimaryKey() and not s.derived
	    
	    if (not s.uuid.isDefined() or s.uuid.isEmpty()) {
	        u.uuid  = generateUuid(generateUuid(s.ownerClass.uuid, s.name, "."), "uniqueConstraint", "_");
	    } else {
	        u.uuid = generateUuid(s.uuid, "uniqueConstraint", "_");
	    }
		u.table = s.ownerClass.equivalent("JClassToRdbmsTable");
		// u.sqlName = 'UQ_' + u.table.sqlName + "_" + s.fieldSqlName();
		/*if (("UQ_" + u.uuid.replaceAll("-","")).length > 30) {
			u.sqlName = ("UQ_" + u.uuid.replaceAll("-","")).substring(0,30);
		} else {
			u.sqlName = ("UQ_" + u.uuid.replaceAll("-",""));
		}*/
		u.sqlName = ("UQ_" + md5(u.uuid.replaceAll("-","")));
		
		u.name = s.ownerClass.package.fqName() + "." + s.ownerClass.name + "." + s.name;
		u.description = s.ownerClass.name + "." + s.name;
		u.fields.add(s.equivalent("JAttributeToTableValueField"));
		
		//	    ('Unique found U_' + u.table.sqlName + "_" + s.fieldSqlName()).println();
	    //u.table.uniqueKeys.add(u);
}

