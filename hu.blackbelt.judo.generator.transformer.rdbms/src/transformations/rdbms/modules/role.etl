import "../../../operations/_importAll.eol";
import "element.etl";

rule JRoleToRdbmsTableForeignKey 
	transform s : PSM!JRole
	to t : RDBMS!RdbmsForeignKey {
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and s.isForeign() and not s.ownerRelationship.isJunction()
	    t.name = s.name;
	    t.referenceKey = s.opposite().ownerClass.equivalent().primaryKey;
	    t.sqlName = s.roleSqlName();	    
		t.table =  s.ownerClass.equivalent("JClassToRdbmsTable");
		t.type = idType();
		t.description =  s.name + "(" + s.opposite().ownerClass.name + ")";
		t.uuid = s.uuid;
		t.mandatory = s.isMandatory();
	}

rule JRoleToRdbmsViewIdentifierForeignKey 
	transform s : PSM!JRole
	to v : RDBMS!RdbmsViewIdentifierField {
		guard : s.ownerClass.participates(PSM!JLayer#PERSISTENCE) and s.isForeign() and not s.ownerRelationship.isJunction()
	    v.name = s.name;
	    v.sqlName = s.roleSqlName();
	    v.view = s.ownerClass.equivalent("JClassToRdbmsView");
		v.description =  s.name + "(" + s.opposite().ownerClass.name + ")";
		v.uuid = uuid3(s.uuid, "view");
		v.tableField = s.equivalent("JRoleToRdbmsTableForeignKey");
	}

