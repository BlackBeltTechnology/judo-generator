operation transformEnum(annotation : pimEXT!EAnnotation) {
	var extEnum = annotation.eModelElement;
	var pimEnum = new inpPIM!Enumeration();
	pimEnum.name = extEnum.name;
			
	for (literal in extEnum.eLiterals) {
		var newLiteral = new inpPIM!EnumerationLiteral();
		newLiteral.name = literal.name;
		
		pimEnum.ownedLiteral.add(newLiteral);
	}
			
	var pimPackage = inpPIM!Package.all.selectOne(package | package.fqName() = extEnum.ePackage.fqName());
	pimPackage.packagedElement.add(pimEnum);
	pimEnum.addStereotypes(annotation);
}

operation inpPIM!Enumeration addStereotypes(annotation : pimEXT!EAnnotation) {
	var const;
	if (annotation.details.get("fixed").isDefined() and annotation.details.get("fixed") = "true") {
		const = new inpPIM!fixed();
		const.base_Enumeration = self;
	}
	
	if (annotation.details.get("read").isDefined()) {
        self.getOrAddentityAccessDef().Read = transfromPrivStrings(annotation.details.get("read"));
	}
    
    if (annotation.details.get("list").isDefined()) {
        self.getOrAddentityAccessDef().`List` = transfromPrivStrings(annotation.details.get("list"));
	}
    
    if (annotation.details.get("edit").isDefined()) {
        self.getOrAddentityAccessDef().Edit = transfromPrivStrings(annotation.details.get("edit"));
	}
}

operation inpPIM!Enumeration getOrAddentityAccessDef () : inpPIM!EntityAccessDef {
    var ead = inpPIM!EntityAccessDef.allInstances()->selectOne(c | c.base_Enumeration = self);
    if (not ead.isDefined()) {
        ead = new inpPIM!EntityAccessDef;
        ead.base_Enumeration = self;
    }
    return ead;
}