[%
import "operations/_importAll.eol";

for (package : JAVA!JvmPackage in getPackages()) {
	package.swaggerFileName().println("Package: ");

    var template = TemplateFactory.load("templates/swagger/swagger.egl");
    template.populate("dtos", getPackageDtos(package));
    template.populate("definitions", getPackageReferencedDtos(package));
    template.populate("psmVersion", psmVersion);
    template.populate("applicationName", applicationName + " Package: " + package.swaggerTag());
    template.generate("swagger-" + package.swaggerFileName() +".yaml");
    template.reset();

}

operation getAllDtos() : Collection {
	var toBeRemovedCoordinatesHack = Set{
									JAVA!JvmJudoCoordinate#BO_SERVICE_INTERFACE,
									JAVA!JvmJudoCoordinate#BO_SERVICE_ABSTRACT,
									JAVA!JvmJudoCoordinate#BO_SERVICE_DEFAULT,
									JAVA!JvmJudoCoordinate#BO_SERVICE_WRAPPER
									};
									
	var excludedFwElements = FWELEMENTS!JvmClass.all.collect(c | c.swaggerName()); //.select(c | c.uuid.isDefined() and c.origin.isDefined()).collect(c | c.origin);
	
	
	return JAVA!JvmCoordinated.all.select(c | c.coordinate = JAVA!JvmJudoCoordinate#REST_DTO 
				and not (c.framework and toBeRemovedCoordinatesHack.includes(c.coordinate))
				and not (c.coordinate = JAVA!JvmJudoCoordinate#REST_TEST_WRAPPER_C and (c.abstract or c.hasFixedEnum()))
				and not (excludedFwElements.contains(c.origin.swaggerName()))
				);
}

@cached
operation getPackages() : Collection {
	return getAllDtos().collect(d | d.origin.package).asSet();
}

operation getPackageDtos(package : JAVA!JvmPackage) : Collection {
	return getAllDtos().select(d | d.origin.package == package).asSet();
}

operation getPackageReferencedDtos(package : JAVA!JvmPackage) : Collection {
	var ret:List = new List();
	for (c : JAVA!JvmClass in getPackageDtos(package)) {
		ret.addAll(c.allReferencedClass());
	}
	return ret.asSet();
}

%]